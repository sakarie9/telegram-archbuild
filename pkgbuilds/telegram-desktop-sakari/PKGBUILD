# Maintainer: Sakari <20642596+sakarie9@users.noreply.github.com>
# Contributor: lilydjwg <lilydjwg@gmail.com>
# Contributor: Megumi_fox <megumifox@archlinuxcn.org>
# Contributor: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: hexchain <i@hexchain.org>
pkgname=telegram-desktop-sakari
_pkgname=telegram-desktop
pkgver=5.15.3
pkgrel=1
pkgdesc="Official Telegram Desktop client with megumifox & lily's patch"
arch=('x86_64')
url="https://desktop.telegram.org/"
license=('GPL-3.0-or-later WITH OpenSSL-exception')
depends=(
  'abseil-cpp'
  'ada'
  'ffmpeg' 'libavfilter.so' 'libavformat.so' 'libavcodec.so' 'libavutil.so'
  'glib2'
  'hicolor-icon-theme'
  'hunspell'
  'kcoreaddons'
  'libdispatch'
  'libxcomposite'
  'libxdamage'
  'libxrandr'
  'libxtst'
  'libvpx' 'libvpx.so'
  'lz4'
  'minizip'
  'openal'
  'openh264' 'libopenh264.so'
  'openssl' 'libcrypto.so' 'libssl.so'
  'pipewire'
  'protobuf' 'libprotobuf-lite.so'
  'qt6-base=6.9.1'
  'qt6-imageformats'
  'qt6-svg'
  'qt6-wayland'
  'rnnoise'
  'xxhash'
  'libavif'
  'libheif'
)
makedepends=(
  'boost'
  'cmake'
  'git'
  'glib2-devel'
  'gobject-introspection'
  'gperf'
  'libtg_owt'
  'microsoft-gsl'
  'ninja'
  'python'
  'range-v3'
  'tl-expected'
)
optdepends=(
  'geoclue: geoinformation support'
  'geocode-glib-2: geocoding support'
  'geocode-glib: geocoding support'
  'webkit2gtk-4.1: embedded browser features provided by webkit2gtk-4.1'
  'webkit2gtk: embedded browser features provided by webkit2gtk'
  'webkitgtk-6.0: embedded browser features provided by webkitgtk-6.0 (Wayland only)'
  'xdg-desktop-portal: desktop integration'
)
provides=('telegram-desktop')
conflicts=('telegram-desktop')
options=(!debug)
_td_commit=51743dfd01dff6179e2d8f7095729caa4e2222e9
source=(
  "https://github.com/telegramdesktop/tdesktop/releases/download/v${pkgver}/tdesktop-${pkgver}-full.tar.gz"
  "git+https://github.com/tdlib/td.git#tag=${_td_commit}"
  "no-embolded-font.patch"
  "dont-adjust-fontsize.patch"
  "0001-add-TDESKTOP_DISABLE_REGISTER_CUSTOM_SCHEME-back.patch"
  "0001-don-t-ask-which-program-to-use-when-opening-a-file-w.patch"
  "0002-make-blockquote-markup-less-distractive.patch"
  "0004-make-animated-avatar-follow-power-settings.patch"
  "0009-Disable-sponsored-messages.patch"
  "0010-Option-to-disable-stories.patch"
)

sha512sums=('b71ad402834fdd73b5fdc44ba7a0f99c046494822deba6b143d3f79e7d2f835528f831106946f0048f6d8460f942a850c3ad2eb602810c78d2c9b76e9a45d5a4'
  'd622b8f3580ee49415546d025c4ba45f5b2de50b315fc379dc57c0427c5f815c7cc3820cca937c12182ee461641bb61f87ebc99b6c74a1a666cea9a08f0f41a0'
  '1c5ace38926fe8e2c2dfec6b0d9073c77031fc15b60220ffcad626a0f933d6d71d31bdb7e1ccb8bfaa76c1d0338ddd2193a2e75ac1623dd66bbc8e3ee9be4885'
  'a5cc0b821d493333ee71957e71be51fb753bb3c0b97ca44b63ee1f2c93fa5ea2d8dda0560a37cbe55bb9dd17b557ab8e95725cafe32e6bec1989bcd3f42945c6'
  '35660ffbb5061835fceab2e7ab613b60ab26533e8a03bcd951da2e046e2d13c227138628fea6ff04655b04605576f9f36b5fea9b10012c7cefe03126afaab6fe'
  '653945cb6e6425fb4631b4c98f47d4769b069e205be822156a8c455dbddd65f6ff38b57f279350a1891a16c53d2623564803274a8700fc4573b4ba3810979766'
  '9d2e7512734044682a18b79cdda4fd21aa1427c4e066c9288e8218e890744225ff19114a5a129e861a9d382fb5dae0bf19e82e0e9ca862c112ad9a2b54a853d0'
  '871fd0994bb35dcf32d3d2cbf222db92c30acfd76df06fe87feecbac205f08e3bbed8e48a56c4e11737638fafddc12a1e278519481f77f7dab3f9957ea6e61ed'
  '23691e85c827d330524feae52cec41c9783ee50263c03b5494b7cc338e8a3a7f357567098c1c992fcce6c34163ee812ca0dfa761e8068e606a10487a950850d9'
  'f683335508108fdd31b0a9d934d3847d60a1610a2c5a8ea1b64c2b1fb0c88fadd7d0e3ef706c8a5b1cb18180290259515a983c6fb67749cb24efd7296f4ef8ad')

prepare() {
  patch -b -l -d tdesktop-$pkgver-full/Telegram/lib_ui -Np1 -i ${srcdir}/no-embolded-font.patch
  patch -b -l -d tdesktop-$pkgver-full/Telegram/lib_ui -Np1 -i ${srcdir}/dont-adjust-fontsize.patch
  patch -b -l -d tdesktop-$pkgver-full/ -Np1 -i ${srcdir}/0001-add-TDESKTOP_DISABLE_REGISTER_CUSTOM_SCHEME-back.patch
  patch -b -l -d tdesktop-$pkgver-full/ -Np1 -i ${srcdir}/0001-don-t-ask-which-program-to-use-when-opening-a-file-w.patch
  patch -b -l -d tdesktop-$pkgver-full/ -Np1 -i ${srcdir}/0002-make-blockquote-markup-less-distractive.patch
  patch -b -l -d tdesktop-$pkgver-full/ -Np1 -i ${srcdir}/0004-make-animated-avatar-follow-power-settings.patch

  patch -b -l -d tdesktop-$pkgver-full/ -Np1 -i ${srcdir}/0009-Disable-sponsored-messages.patch
  patch -b -l -d tdesktop-$pkgver-full/ -Np1 -i ${srcdir}/0010-Option-to-disable-stories.patch

  sed -i 's/QGenericUnixServices/QDesktopUnixServices/;s/qgenericunixservices_p/qdesktopunixservices_p/' \
    ${srcdir}/tdesktop-*-full/Telegram/lib_base/base/platform/linux/base_linux_xdp_utilities.cpp
}

build() {
  CXXFLAGS+=' -ffat-lto-objects'

  cmake -S td -B td/build \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX="$PWD/td/install" \
    -Wno-dev \
    -DTD_E2E_ONLY=ON
  cmake --build td/build
  cmake --install td/build

  # Turns out we're allowed to use the official API key that telegram uses for their snap builds:
  # https://github.com/telegramdesktop/tdesktop/blob/8fab9167beb2407c1153930ed03a4badd0c2b59f/snap/snapcraft.yaml#L87-L88
  # Thanks @primeos!
  cmake -B build -S tdesktop-$pkgver-full -G Ninja \
    -DCMAKE_VERBOSE_MAKEFILE=ON \
    -DCMAKE_INSTALL_PREFIX="/usr" \
    -Dtde2e_DIR="$PWD/td/install/lib/cmake/tde2e" \
    -DCMAKE_BUILD_TYPE=Release \
    -DTDESKTOP_API_ID=611335 \
    -DTDESKTOP_API_HASH=d524b414d21f4d37f08684c1df41ac9c \
    -DTDESKTOP_DISABLE_REGISTER_CUSTOM_SCHEME=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
